plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'com.github.hierynomus.license' version '0.12.1'
}

// Project properties
group = 'jbyoshi'
archivesBaseName = 'gitupdate'
version = '1.2-SNAPSHOT'

// Java 1.8
sourceCompatibility = targetCompatibility = '1.8'

// Project dependencies
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://repo.eclipse.org/content/groups/releases/'
    }
}
dependencies {
    compile "org.eclipse.jgit:org.eclipse.jgit:4.2.0.201511101648-m1"
    compile "com.google.guava:guava:18.0"
    runtime "org.slf4j:slf4j-simple:1.7.10" // Required by JGit
}

// License
defaultTasks 'licenseFormat', 'build'
license {
    header file('gradle/HEADER.txt')
    include '**/*.java'

    ignoreFailures false
    strictCheck true

    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}
processResources {
    // Include in final JAR
    from 'LICENSE.txt'
}

// Application+Shadow
mainClassName = 'jbyoshi.gitupdate.GitUpdate'
shadowJar {
    dependencies {
        exclude(dependency('org.eclipse.jdt:org.eclipse.jdt.annotation'))
    }
    classifier = ''
}
build.dependsOn shadowJar
